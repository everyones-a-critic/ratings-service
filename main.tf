terraform {
  required_providers {
    mongodbatlas = {
      source  = "mongodb/mongodbatlas"
      version = "1.4.5"
    }
  }

  cloud {
    organization = "everyones-a-critic"
    workspaces {
      name = "ratings-service"
    }
  }
}

provider "mongodbatlas" {}

provider "aws" {
  region = var.region
}

data "tfe_outputs" "ecr_repositories" {
  organization = "everyones-a-critic"
  workspace    = "ecr-repositories"
}

data "tfe_outputs" "api_gateway" {
  organization = "everyones-a-critic"
  workspace    = "api-gateway"
}

data "tfe_outputs" "mongo_db" {
  organization = "everyones-a-critic"
  workspace    = "mongo-db"
}

# Creating Mongo Resources
resource "mongodbatlas_serverless_instance" "main" {
  project_id = data.tfe_outputs.mongo_db.values.project_id
  name       = "ratings-service-${var.environment}"

  provider_settings_backing_provider_name = "AWS"
  provider_settings_provider_name         = "SERVERLESS"
  provider_settings_region_name           = var.mongo_region
}

resource "mongodbatlas_database_user" "api_user" {
  username           = "api_user"
  password           = var.mongo_user_password
  project_id         = data.tfe_outputs.mongo_db.values.project_id
  auth_database_name = "admin"

  roles {
    role_name     = "readWriteAnyDatabase"
    database_name = "admin"
  }

  scopes {
    name = mongodbatlas_serverless_instance.main.name
    type = "CLUSTER"
  }
}

resource "aws_vpc" "main" {
  cidr_block = var.cidr
  tags = {
    Name : "${var.name}-vpc-${var.environment}"
  }
}

resource "aws_internet_gateway" "main" {
  vpc_id = aws_vpc.main.id
}

resource "aws_subnet" "private" {
  vpc_id            = aws_vpc.main.id
  cidr_block        = element(var.private_subnets, count.index)
  availability_zone = element(var.availability_zones, count.index)
  count             = length(var.private_subnets)
}

resource "aws_subnet" "public" {
  vpc_id                  = aws_vpc.main.id
  cidr_block              = element(var.public_subnets, count.index)
  availability_zone       = element(var.availability_zones, count.index)
  count                   = length(var.public_subnets)
  map_public_ip_on_launch = true
}

resource "aws_route_table" "public" {
  vpc_id = aws_vpc.main.id
}

resource "aws_route" "public" {
  route_table_id         = aws_route_table.public.id
  destination_cidr_block = "0.0.0.0/0"
  gateway_id             = aws_internet_gateway.main.id
}

resource "aws_route_table_association" "public" {
  count          = length(var.public_subnets)
  subnet_id      = element(aws_subnet.public.*.id, count.index)
  route_table_id = aws_route_table.public.id
}

resource "aws_nat_gateway" "main" {
  count         = length(var.private_subnets)
  allocation_id = element(aws_eip.nat.*.id, count.index)
  subnet_id     = element(aws_subnet.public.*.id, count.index)
  depends_on    = [aws_internet_gateway.main]
}

resource "aws_eip" "nat" {
  count = length(var.private_subnets)
  vpc   = true
}

resource "aws_route_table" "private" {
  count  = length(var.private_subnets)
  vpc_id = aws_vpc.main.id
}

resource "aws_route" "private" {
  count                  = length(compact(var.private_subnets))
  route_table_id         = element(aws_route_table.private.*.id, count.index)
  destination_cidr_block = "0.0.0.0/0"
  nat_gateway_id         = element(aws_nat_gateway.main.*.id, count.index)
}

resource "aws_route_table_association" "private" {
  count          = length(var.private_subnets)
  subnet_id      = element(aws_subnet.private.*.id, count.index)
  route_table_id = element(aws_route_table.private.*.id, count.index)
}

resource "aws_security_group" "alb" {
  name   = "${var.name}-sg-alb-${var.environment}"
  vpc_id = aws_vpc.main.id

  ingress {
    protocol         = "tcp"
    from_port        = 80
    to_port          = 8080
    cidr_blocks      = ["0.0.0.0/0"]
    ipv6_cidr_blocks = ["::/0"]
  }

  ingress {
    protocol         = "tcp"
    from_port        = 443
    to_port          = 443
    cidr_blocks      = ["0.0.0.0/0"]
    ipv6_cidr_blocks = ["::/0"]
  }

  egress {
    protocol         = "-1"
    from_port        = 0
    to_port          = 0
    cidr_blocks      = ["0.0.0.0/0"]
    ipv6_cidr_blocks = ["::/0"]
  }
}

resource "aws_security_group" "ecs_tasks" {
  name   = "${var.name}-sg-task-${var.environment}"
  vpc_id = aws_vpc.main.id

  ingress {
    protocol         = "tcp"
    from_port        = var.container_port
    to_port          = var.container_port
    cidr_blocks      = ["0.0.0.0/0"]
    ipv6_cidr_blocks = ["::/0"]
  }

  egress {
    protocol         = "-1"
    from_port        = 0
    to_port          = 0
    cidr_blocks      = ["0.0.0.0/0"]
    ipv6_cidr_blocks = ["::/0"]
  }
}

resource "aws_ecs_cluster" "main" {
  name = "${var.name}-cluster-${var.environment}"
}

resource "aws_ecs_task_definition" "main" {
  family                   = var.name
  network_mode             = "awsvpc"
  requires_compatibilities = ["FARGATE"]
  cpu                      = 256
  memory                   = 512
  execution_role_arn       = aws_iam_role.ecs_task_execution_role.arn
  container_definitions = jsonencode([{
    name      = "${var.name}-container-${var.environment}"
    image     = "${data.tfe_outputs.ecr_repositories.values.ratings_service_repository_url}"
    essential = true
    portMappings = [{
      protocol      = "tcp"
      containerPort = var.container_port
      hostPort      = var.container_port
    }],
    environment = [
      {
          name = "MONGO_CLUSTER_NAME",
          value = mongodbatlas_serverless_instance.main.name
      },
      {
          name = "MONGO_USERNAME",
          value = mongodbatlas_database_user.api_user.username
      },
      {
          name = "MONGO_PASSWORD",
          value = var.mongo_user_password
      },
    ]
  }])
}

resource "aws_iam_role" "ecs_task_execution_role" {
  name = "${var.name}-task-execution"

  assume_role_policy = <<EOF
{
 "Version": "2012-10-17",
 "Statement": [
   {
     "Action": "sts:AssumeRole",
     "Principal": {
       "Service": "ecs-tasks.amazonaws.com"
     },
     "Effect": "Allow",
     "Sid": ""
   }
 ]
}
EOF
}

resource "aws_iam_role_policy_attachment" "ecs-task-execution-role-policy-attachment" {
  role       = aws_iam_role.ecs_task_execution_role.name
  policy_arn = "arn:aws:iam::aws:policy/service-role/AmazonECSTaskExecutionRolePolicy"
}

resource "aws_ecs_service" "main" {
  name                               = "${var.name}-${var.environment}"
  cluster                            = aws_ecs_cluster.main.id
  task_definition                    = aws_ecs_task_definition.main.arn
  desired_count                      = 2
  deployment_minimum_healthy_percent = 50
  deployment_maximum_percent         = 200
  launch_type                        = "FARGATE"
  scheduling_strategy                = "REPLICA"

  network_configuration {
    security_groups  = ["${aws_security_group.ecs_tasks.id}"]
    subnets          = aws_subnet.private.*.id
    assign_public_ip = false
  }

  load_balancer {
    target_group_arn = aws_alb_target_group.main.id
    container_name   = "${var.name}-container-${var.environment}"
    container_port   = var.container_port
  }

  lifecycle {
    ignore_changes = [task_definition, desired_count]
  }
}

resource "aws_lb" "main" {
  name               = "${var.name}-alb-${var.environment}"
  internal           = true
  load_balancer_type = "network"

  subnets = aws_subnet.public.*.id

  enable_deletion_protection = false
}

resource "aws_alb_target_group" "main" {
  name        = "${var.name}-tg-${var.environment}"
  port        = 8080
  protocol    = "TCP"
  vpc_id      = aws_vpc.main.id
  target_type = "ip"
  health_check {
    enabled = true
    path = var.health_check_path
  }
}

resource "aws_alb_listener" "tcp" {
  load_balancer_arn = aws_lb.main.id
  port              = 80
  protocol          = "TCP"

  default_action {
    target_group_arn = aws_alb_target_group.main.id
    type             = "forward"
  }
}

resource "aws_security_group" "vpc_link" {
  name   = "${var.name}-vpc-link-sg-${var.environment}"
  vpc_id = aws_vpc.main.id

  ingress {
    protocol         = "-1"
    from_port        = 0
    to_port          = 0
    cidr_blocks      = ["0.0.0.0/0"]
    ipv6_cidr_blocks = ["::/0"]
  }
}

resource "aws_api_gateway_vpc_link" "main" {
  name        = "${var.name}-vpc_link-${var.environment}"
  target_arns = [aws_lb.main.arn]
}

# resource "aws_api_gateway_method" "get-root" {
#   rest_api_id   = data.tfe_outputs.api_gateway.values.gateway_id
#   resource_id   = data.tfe_outputs.api_gateway.values.root_resource_id
#   authorization = "NONE"
#   http_method   = "GET"

#   request_parameters = {
#     "method.request.querystring.name" = false
#   }

#   depends_on = [aws_api_gateway_vpc_link.main]

# }

# resource "aws_api_gateway_model" "community" {
#   rest_api_id  = aws_api_gateway_rest_api.main.id
#   name         = "community"
#   content_type = "application/json"

#   schema = <<EOF
# {
#   "$schema": "http://json-schema.org/draft-04/schema#",
#   "type": "object",
#   "properties": {
#     "id": {
#       "type": "integer"
#     },
#     "name": {
#       "type": "string"
#     }
#   },
#   "required": [
#     "id",
#     "name"
#   ]
# }
# EOF
# }

# resource "aws_api_gateway_method_response" "response_200" {
#   depends_on  = [aws_api_gateway_method.get-root]
#   rest_api_id = aws_api_gateway_rest_api.main.id
#   resource_id = aws_api_gateway_rest_api.main.root_resource_id
#   http_method = aws_api_gateway_method.get-root.http_method
#   status_code = "200"

#   response_models = {
#     "application/json" : aws_api_gateway_model.community.name
#   }
# }

# resource "aws_api_gateway_integration" "main" {
#   rest_api_id = data.tfe_outputs.api_gateway.values.gateway_id
#   resource_id = aws_api_gateway_rest_api.main.root_resource_id
#   http_method = aws_api_gateway_method.get-root.http_method

#   type                    = "HTTP_PROXY"
#   uri                     = "http://eac-community-service-alb-prod-69f50d3da3fe103d.elb.us-west-1.amazonaws.com"
#   integration_http_method = "GET"

#   connection_type = "VPC_LINK"
#   connection_id   = aws_api_gateway_vpc_link.main.id
# }

